buildscript {
    ext {
        buildToolsVersion = "36.0.0"
        minSdkVersion = 24
        compileSdkVersion = 36
        targetSdkVersion = 36
        ndkVersion = "27.1.12297006"
        kotlinVersion = "2.1.20"
        REACT_NATIVE_NODE_MODULES_DIR = "${rootDir}/../node_modules/react-native"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin")
        classpath("com.google.gms:google-services:4.4.0")
    }
}
    subprojects {
        // Force consistent GMS play-services-location version to fix
        // IncompatibleClassChangeError between react-native-geolocation-service
        // and @react-native-firebase (FusedLocationProviderClient conflict)
        configurations.all {
            resolutionStrategy {
                force 'com.google.android.gms:play-services-location:21.3.0'
            }
        }
        afterEvaluate { project ->
            if (project.hasProperty("android")) {
                project.android {
                    if (namespace == null) {
                        namespace project.group
                    }
                    compileSdkVersion rootProject.ext.compileSdkVersion
                    buildToolsVersion rootProject.ext.buildToolsVersion
                    defaultConfig {
                        minSdkVersion rootProject.ext.minSdkVersion
                        targetSdkVersion rootProject.ext.targetSdkVersion
                    }
                }
            }
        }
    }


apply plugin: "com.facebook.react.rootproject"